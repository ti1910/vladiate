start: "@startuml" (skinparam | class | relationship | ent_attr)* "@enduml"

// Skinparam
skinparam: "skinparam" sparam svalue

// Classes
class: "class" class_name stereotype? ("{" (class_group_title? (column) class_group_title?)* "}")?

// Stereotype
stereotype: "<<" stereotype_name ">>"

// Sections with title
class_group_title: ".." (group_title "..")?
                 | "--" (group_title "--")?
                 | "__" (group_title "__")?
                 | "=="

ent_attr: "note" position "of" class_name (validator)* "end note"

position: position_set

position_set: "right" -> right
            | "left" -> left

column: visibility? name type (" " validator)*

type: type_set

type_set: "string" -> string
        | "int" -> int
        | "bigint" -> bigint
        | "smallint" -> smallint
        | "tinyint" -> tinyint
        | "timestamp" -> timestamp
        | "date" -> date
        | "float" -> float
        | "double" -> double

attribute_set: "tenant_id" -> tenant_id
             | "regexp" -> regexp
             | "set" -> set
             | "list" -> list
             | "created_at" -> created_at
             | "softdelete" -> softdelete
             | "deident" -> deident
             | "size" -> size
             | "mode" -> mode
             | "changes" -> changes
             | "deletion" -> deletion
             | "from" -> from
             | "len" -> len
             | "nodwh" -> nodwh
             | "nostaging" -> nostaging
             | "nocompare" -> nocompare
             | "deprecated" -> deprecated
             | "key" -> key
             | "uuid" -> uuid
             | "decimal" -> decimal
             | "hex" -> hex
             | "float" -> float
             | "json" -> json
             | "str" -> str
             | "null" -> null
             | "json_set" -> json_set
             | "json_list" -> json_list

attribute: attribute_set

validator: attribute "(" [option ("," option)*] ")"
         | attribute

// Relationships
relationship: arrow_end_1 cardinality? arrow_head_1 arrow_body arrow_head_2 cardinality? arrow_end_2 [":" relation_text]

// Arrow Heads
!arrow_head_1: "<"
             | "<|"
             | "*"
             | "o"
             | "+"
             | "#"
             | "x"
             | "}"
             | "^"
             | " "

!arrow_head_2: ">"
             | "|>"
             | "*"
             | "o"
             | "+"
             | "#"
             | "x"
             | "}"
             | "^"
             | " "

!arrow_body: ("-"
           | "--"
           | "---"
           | "-left-"
           | "-up-"
           | "-right-"
           | "-down-"
           | "."
           | ".."
           | ".left."
           | ".up."
           | ".right."
           | ".down." )

cardinality: ESCAPED_STRING

// Variables and Methods visibility
visibility_set: "+" -> public
              | "-" -> private
              | "#" -> protected
              | "~" -> package

visibility: visibility_set

arrow_end_1: CNAME
arrow_end_2: CNAME
name: CNAME
option: (CNAME|INT)
sparam: CNAME
svalue: CNAME|WORD|ESCAPED_STRING|DIGIT
class_name: CNAME
stereotype_name: CNAME
relation_text: (WORD|ESCAPED_STRING|DIGIT|">"|"<")*
group_title: (WORD|ESCAPED_STRING|DIGIT)*

%import common.CNAME
%import common.ESCAPED_STRING
%import common.WORD
%import common.DIGIT
%import common.WS
%import common.INT
%ignore WS
