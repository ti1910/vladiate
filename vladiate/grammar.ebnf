start: "@startuml" (skinparam | class | relationship | meta)* "@enduml"

// Skinparam
skinparam: "skinparam" sparam svalue

// Classes
class: "class" class_name stereotype? ("{" (class_group_title? (column) class_group_title?)* "}")?

// Stereotype
stereotype: "<<" stereotype_name ">>"

// Sections with title
class_group_title: ".." (group_title "..")?
                 | "--" (group_title "--")?
                 | "__" (group_title "__")?
                 | "=="

meta: "note" position "of" class_name (validator)* "end note"

position: position_set

position_set: "right" -> right
        | "left" -> left

column: visibility name type (" " validator)*

type: type_set

type_set: "string" -> string
    | "int" -> int
    | "bigint" -> bigint
    | "smallint" -> smallint
    | "tinyint" -> tyniint
    | "timestamp" -> timestamp
    | "date" -> date
    | "float" -> float
    | "double" -> double

attribute_set: "tenant_id" -> tenant_id
         | "regexp" -> regexp
         | "created_at" -> created_at
         | "softdelete" -> softdelete
         | "deident" -> deident
         | "size" -> size
         | "mode" -> mode
         | "changes" -> changes
         | "deletion" -> deletion

attribute: attribute_set

validator: attribute "(" [option ("," option)*] ")"
         | attribute

// Relationships
relationship: arrow_end_1 cardinality? arrow_head_1 arrow_body arrow_head_2 cardinality? arrow_end_2 [":" relation_text]

// Arrow Heads
!arrow_head_1: "<"
             | "<|"
             | "*"
             | "o"
             | "+"
             | "#"
             | "x"
             | "}"
             | "^"
             | " "

!arrow_head_2: ">"
             | "|>"
             | "*"
             | "o"
             | "+"
             | "#"
             | "x"
             | "}"
             | "^"
             | " "

!arrow_body: ("-"
           | "--"
           | "---"
           | "-left-"
           | "-up-"
           | "-right-"
           | "-down-"
           | "."
           | ".."
           | ".left."
           | ".up."
           | ".right."
           | ".down." )

cardinality: ESCAPED_STRING

// Variables and Methods visibility
visibility: "+" -> public
          | "-" -> private
          | "#" -> protected
          | "~" -> package

arrow_end_1: CNAME
arrow_end_2: CNAME
name: CNAME
option: (CNAME|ESCAPED_STRING|WORD|DIGIT)
sparam: CNAME
svalue: CNAME|WORD|ESCAPED_STRING|DIGIT
class_name: CNAME
stereotype_name: CNAME
relation_text: (WORD|ESCAPED_STRING|DIGIT|">"|"<")*
group_title: (WORD|ESCAPED_STRING|DIGIT)*

%import common.CNAME
%import common.ESCAPED_STRING
%import common.WORD
%import common.DIGIT
%import common.WS
%ignore WS
